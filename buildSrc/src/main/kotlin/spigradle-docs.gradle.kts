import java.net.URL

plugins {
    id("org.jetbrains.dokka")
}

tasks {
    dokkaHtml {
        outputDirectory.set(buildDir.resolve("kdoc"))
        dokkaSourceSets {
            named("main") {
                moduleName.set(project.name)
                jdkVersion.set(8)
                sourceLink {
                    localDirectory.set(file("src/main/kotlin"))
                    remoteUrl.set(URL("https://github.com/spigradle/spigradle/tree/master/src/main/kotlin"))
                    remoteLineSuffix.set("#L")
                }
                externalDocumentationLink {
                    url.set(URL("https://docs.groovy-lang.org/latest/html/gapi/"))
                    packageListUrl.set(URL("https://docs.groovy-lang.org/latest/html/gapi/package-list"))
                }
                externalDocumentationLink {
                    url.set(URL("https://docs.groovy-lang.org/latest/html/gapi/"))
                    packageListUrl.set(URL("https://docs.gradle.org/current/javadoc/package-list"))
                }
                externalDocumentationLink {
                    url.set(URL("https://javadoc.io/doc/com.fasterxml.jackson.dataformat/jackson-dataformat-yaml/latest/"))
                    packageListUrl.set(URL("https://javadoc.io/doc/com.fasterxml.jackson.dataformat/jackson-dataformat-yaml/latest/package-list"))
                }
                externalDocumentationLink {
                    url.set(URL("https://hub.spigotmc.org/javadocs/spigot/"))
                    packageListUrl.set(URL("https://hub.spigotmc.org/javadocs/spigot/package-list"))
                }
            }
        }
    }

    val updateTemplateDocs by tasks.registering {
        group = "spigradle build"
        fun CopySpec.configure() {
            expand(
                    "GRADLE_VERSION" to gradle.gradleVersion,
                    "SPIGRADLE_VERSION" to project.version,
                    "KOTLIN_VERSION" to "2.1.20",
                    "version" to "\$version"
            )
            filter { line ->
                if (line.contains(Regex("^#[#]?[#]? "))) """
                $line
                
                [comment]: <> (!! Do not edit this file but 'docs/templates' or 'docs/root-templates', See [CONTRIBUTING.md] !!)
            """.trimIndent() else line
            }
            rename { name ->
                name.replace(Regex("^template_"), "")
            }
        }
        doLast {
            val docsDir = project.rootDir.resolve("docs")
            copy {
                from(docsDir.resolve("templates"))
                include("*.md")
                into(docsDir)
                configure()
            }
            copy {
                from(docsDir.resolve("root-templates"))
                include("*.md")
                into(project.rootDir)
                configure()
            }
        }
    }
}