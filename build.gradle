plugins {
    id 'groovy'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'com.gradle.plugin-publish' version '0.10.1'
}

group 'kr.entree'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.25'
    implementation group: 'org.ow2.asm', name: 'asm', version: '7.2'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

def packageName = "${project.group}.spigradle"

shadowJar {
    def prefix = "${packageName}.libs"
    [
            'org.yaml.snakeyaml': 'snakeyaml',
            'org.objectweb.asm' : 'asm'
    ].each {
        relocate(it.key, "${prefix}.$it.value")
    }
    archiveClassifier.set("")
    minimize()
}

pluginBundle {
    website = 'https://github.com/EntryPointKR/Spigradle'
    vcsUrl = 'https://github.com/EntryPointKR/Spigradle'
    tags = ['spigot', 'minecraft']
    plugins {
        spigradle {
            id = 'kr.entree.spigradle'
            displayName = 'Spigradle'
            description = 'Gradle plugin for Spigot plugin development.'
        }
    }
}

tasks.build.dependsOn shadowJar
