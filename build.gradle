plugins {
    id 'groovy'
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.gradle.plugin-publish' version '0.10.1' apply false
    id 'com.jfrog.bintray' version '1.8.4' apply false
}

group 'kr.entree'
version '1.2.1-SNAPSHOT'
description = 'Gradle plugin for developing Spigot plugin.'

['publish', 'generateMeta'].each {
    def buildFileName = "gradle/${it}.gradle"
    if (file(buildFileName).isFile()) {
        apply from: buildFileName
    } else {
        apply from: "${buildFileName}.kts"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    shadow gradleApi()
    shadow localGroovy()
    shadow 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.yaml:snakeyaml:1.25'
    implementation 'org.ow2.asm:asm:7.2'
    testImplementation 'junit:junit:4.12'
}

final def PACKAGE_NAME = "${project.group}.spigradle"

shadowJar {
    def prefix = "${PACKAGE_NAME}.libs"
    [
            'org.yaml.snakeyaml': 'snakeyaml',
            'org.objectweb.asm' : 'asm'
    ].each {
        relocate(it.key, "${prefix}.$it.value")
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    archiveClassifier.set("")
    minimize()
}

jar {
    dependsOn shadowJar
    enabled = false
}

// Do not make relation with java
compileGroovy.classpath = sourceSets.main.compileClasspath
// Include groovy classpath into Kotlin compiler
compileKotlin.classpath += files(sourceSets.main.groovy.classesDirectory)
